apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'joda-time:joda-time:2.10.5'

    implementation 'org.jetbrains:annotations:18.0.0'

    testImplementation 'junit:junit:4.12'
}

def isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.orgzly'
            artifactId = 'org-java'
            version = VERSION_NAME

            pom {
                name = 'org-java'
                packaging = 'jar'
                description = 'Org files parser and generator'
                url = 'https://github.com/orgzly/org-java'

                scm {
                    url = "http://github.com/orgzly/org-java"
                    connection = "scm:git:http://github.com/orgzly/org-java.git"
                    developerConnection = "scm:git:git@github.com:orgzly/org-java.git"
                }

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'neven'
                        name = 'Neven'
                        email = 'neven@orgzly.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = isReleaseBuild() ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/' : 'https://oss.sonatype.org/content/repositories/snapshots/'

            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign publishing.publications.mavenJava
}